plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
    id 'org.jetbrains.kotlin.jvm' version '1.7.0'
}

group = 'com.hibiscusmc'
version = '0.1-SNAPSHOT'
description = "Write something here idk.\n"


repositories {
    mavenCentral()
    gradlePluginPortal()
    maven {
        name = 'spigotmc-repo'
        url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'
    }
    maven {
        name = 'sonatype'
        url = 'https://oss.sonatype.org/content/groups/public/'
    }
    maven {
        name = 'mineinabyss-repo'
        url = 'https://repo.mineinabyss.com/releases/'
    }
    maven {
        name = 'jitpack-repo'
        url = 'https://jitpack.io'
    }
}

dependencies {
    compileOnly 'org.spigotmc:spigot-api:1.18-R0.1-SNAPSHOT'
    compileOnly "com.oraxen:oraxen:1.139.1" //TODO Change this of MineInAbyss repo
    compileOnly "com.github.LoneDev6:api-itemsadder:3.2.3c"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    implementation "net.kyori:adventure-api:4.11.0"
    implementation 'net.kyori:adventure-text-minimessage:4.11.0'
    implementation "dev.triumphteam:triumph-gui:3.1.3"
    implementation "me.mattstudios.utils:matt-framework:1.4.6"
}

def targetJavaVersion = 17
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release = targetJavaVersion
    }
}

boolean copyJar = project.findProperty('copyJar')
String pluginPath = project.findProperty('hibiscusmc_plugin_path')
tasks {
    shadowJar {
        relocate("dev.triumphteam.gui", "com.hibiscusmc.hmccolor.gui")
        relocate("me.mattstudios.mf", "com.hibiscusmc.hmccolor.mf")
        relocate("net.kyori.adventure", "com.hibiscusmc.hmccolor.adventure")
        relocate("org.spongepowered.configurate", "com.hibiscusmc.hmccolor.configurate")
        relocate("org.bstats", "com.hibiscusmc.hmccolor.bstats")
        archiveFileName.set("HMCColor.jar")
    }

    task copyJarTask(type: Copy) {
        from 'build/libs/HMCColor.jar'
        into pluginPath
        doLast {
            println('Copied to plugin directory ' + pluginPath)
        }
    }

    copyJarTask.dependsOn('shadowJar')

    build {
        dependsOn('copyJarTask')
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}
compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}
